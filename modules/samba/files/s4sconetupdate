#! /usr/bin/perl -w
use strict;
use warnings;
use Text::Unidecode;
use MIME::Base64;
use Encode;
use File::Basename;
use Getopt::Long;
use String::Random;
use experimental 'smartmatch';

# output as utf8 #
binmode(STDOUT,':encoding(UTF-8)');

#########################
#  check command input  #
#########################

# usage #
sub displayUsage {
   my $callerProgram = basename($0);
   my $usageString = <<"EOF";

usage : $callerProgram [options] sconetFile

Description :

   Update the SAMBA database from the Sconet XML file.

Options :

   -n, --non-interactive : disable prompts
   -u, --update-data : update students data (surname, givenname, ...) from XML, default no
   -d, --delete-students : delete students absent from the sconet file, default no
   -s, --script-output=<path> : instead of update the database, create
                                a script to do it
   -p, --password-file=<path> : a file where the password are stored,
                                it can be contain input login and passwords in the CSV form :
                                Class,SurName,GivenName,<InputLogin>,<InputPassword>
   -r, --random-password : create random passwords, default = same as login


EOF

   print($usageString);
   exit(0);
}

# the options #
my $optNoInteractive;
my $optUpdateData;
my $optDeleteStudents;
my $optUpdateScript;
my $optPasswordFile;
my $optRandomPassword;

Getopt::Long::Configure("bundling","no_ignore_case");
GetOptions("n|no-interactive" => \$optNoInteractive,
           "u|update-data" => \$optUpdateData,
           "d|delete-students" => \$optDeleteStudents,
           "s|update-script=s" => \$optUpdateScript,
           "p|password-file=s" => \$optPasswordFile,
           "r|random-password" => \$optRandomPassword)
          or displayUsage();

# check sconet file #
if( @ARGV != 1 ){
   displayUsage();
}

# check arg #
if( ! -e $ARGV[0] ) {
   print("Sconet file \"$ARGV[0]\" not found ! \n");
   exit(0);
}    


# ask question if no interactive #
sub askYesOrNo {

   # get params, the questions #
   my $question = $_[0];

   # if no interactive return yes #
   if( defined($optNoInteractive) ) {
      return 'yes';
   }

   # ask #
   while( 1 ) {

      print("$question ([y]es or [N]o): ");
      my $rep = <STDIN>;
      if( $rep =~ m/^y(es|)$/ ) {
         return 'yes';
      }
      elsif( $rep =~ m/^no?$/ ) {
         return 'no';
      }
   }
}


######################
# check samba config #
######################
my $s4ConfFile = "/etc/samba/s4.conf";

# global vars #
my $samDatabase;
my $studentBaseDN;
my $baseDN;
my $baseUserDN;
my $baseStudentDN;
my $loginSurnameChar;
my $loginGivennameChar;
my $unixHomePath;
my $baseStudentDir;
my $teachersDirName;

# open main configuration file #
open(my $sambaConf, "<:encoding(UTF-8)", "$s4ConfFile") or die("Cannot read $s4ConfFile");
while( my $line = <$sambaConf> ) {

   chomp($line);
   
   # remove comments #
   if( $line =~ m/\#/ ) {
      if( $line =~ /(.*?)\#/ ) {
         $line = $1;
      }
   }

   # sam database #
   if ( $line =~ /samDatabase *= *\"(.*?)\"/ ) {
      $samDatabase = $1;
   }

   # student base dn #
   if ( $line =~ /baseDN *= *\"(.*?)\"/ ) {
      $baseDN = $1;
   }
   if( $line =~ /baseUserDN *= *\"(.*?)\"/ ) {
      $baseUserDN = $1;
   }
   if ( $line =~ /baseStudentDN *= *\"(.*?)\"/ ) {
      $baseStudentDN = $1;
   }

   # login surname part #
   if ( $line =~ /loginSurnameChar *= *\"(.*?)\"/ ) {
      $loginSurnameChar = $1;
   }

   # login givenname part #
   if ( $line =~ /loginGivennameChar *= *\"(.*?)\"/ ) {
      $loginGivennameChar = $1;
   }

   # unix home path #
   if ( $line =~ /unixHomePath *= *\"(.*?)\"/ ) {
      $unixHomePath = $1;
   }

   # base students directory #
   if ( $line =~ /baseStudentDir *= *\"(.*?)\"/ ) {
      $baseStudentDir = $1;
   }

   # teacher shared directory name  #
   if ( $line =~ /teachersDirName *= *\"(.*?)\"/ ) {
      $teachersDirName = $1;
   }

}
close($sambaConf);


# check global vars #
if( ! (defined($samDatabase) &&
       defined($baseDN) &&
       defined($baseStudentDN) &&
       defined($loginSurnameChar) &&
       defined($loginGivennameChar) ) ) {
   print("Cannot read global variables on $s4ConfFile\n");
   exit(1); 
} else {
   $studentBaseDN = "$baseStudentDN,$baseDN";
}


###################################
#  if interactive, query options  #
###################################

# check if the user want to update student's data #
my $checkStudentData;

if( defined($optUpdateData) ) { 
   $checkStudentData = 1;
} else {
   if( askYesOrNo("Would you like to update student's data ?") eq "yes" ) {
      $checkStudentData = 1;
   }
}

# check if the user want to delete the students #
my $deleteStudents;

if( defined($optDeleteStudents) ) {
   $deleteStudents = 1;
} else {
   if( askYesOrNo("Would you like to delete the old student's accounts ?") eq "yes" ) {
      $deleteStudents = 1;
   }
}


################
#   local lib  #
################

# save current used login #
my %usedLogin;

sub getAllUsersLogin {

   # get all users #
   open( my $ldbCall, "-|:encoding(UTF-8)", "s4ldbsearch $baseUserDN,$baseDN sub user '()' cn" );
   while( my $userInfoLine = <$ldbCall> ) {

      chomp($userInfoLine);

      # save cn #
      my @userInfo = split(/\t/, $userInfoLine, -1);
      my $userCn = $userInfo[1];
      $usedLogin{$userCn} = 'true';
   }
      
   close($ldbCall);
}

getAllUsersLogin();

# remove special caracters #
sub removeSpecials {
   my ($string) = @_;
   if( ! defined($string) ) {
      return "";
   }

   # remove accents #
   $string = lc(unidecode($string));

   # remove specials caracters #
   $string =~ s/\s//g;
   $string =~ s/(-|')/_/g;
   return $string;
}

# generate login with #
# -> $loginSurnameChar characters + $loginGivennameChar characters
sub getLogin {
   my ($surname, $givename) = @_;

   # remove specials caracters #
   my $baseSurname = removeSpecials($surname);
   my $baseGivenname = removeSpecials($givename);

   # create login #
   my $login = substr($baseSurname, 0, $loginSurnameChar) . substr($baseGivenname, 0, $loginGivennameChar);
   
   # remove double underscore #
   $login =~ s/_{2,}/_/g;

   return $login;
}




#######################
#  LDB base           #
#  getting students   #
#######################

sub GetLDBStudents {

   # get all students #
   open(my $ldbCall, "-|:encoding(UTF-8)", "s4ldbsearch $studentBaseDN sub user '()' cn sn givenName serialNumber employeeNumber displayName telephoneNumber mail") or die("Cannot launch s4ldbsearch command !\n");

   # build the user list #
   my @ldbusers;
   my $userIdx = 0;
   while( my $userInfoLine = <$ldbCall> ) {

      chomp($userInfoLine);

      my @userInfo = split(/\t/, $userInfoLine, -1);
      my @userDNParts = split(/,/, $userInfo[0], -1);

      $ldbusers[$userIdx]{'class'} = substr($userDNParts[1], length('OU='));
      $ldbusers[$userIdx]{'login'} = $userInfo[1];
      $ldbusers[$userIdx]{'surName'} = $userInfo[2];
      $ldbusers[$userIdx]{'givenName'} = $userInfo[3];
      $ldbusers[$userIdx]{'id'} = lc($userInfo[4]);
      $ldbusers[$userIdx]{'localId'} = lc($userInfo[5]);
      $ldbusers[$userIdx]{'completeName'} = $userInfo[6];
      $ldbusers[$userIdx]{'telephoneNumber'} = $userInfo[7];
      $ldbusers[$userIdx]{'mail'} = lc($userInfo[8]);
      
      # next user #
      $userIdx++;
   }

   close($ldbCall);
   return @ldbusers;
}


#######################
#  SCONET base        #
#  getting students   #
#######################
use XML::LibXML::Simple;
use XML::SAX::ParserFactory;
use XML::SAX::Base;

sub GetSCONETStudents {
   my ($fileName) = @_;

   # check xml and #
   # get encoding #
   my $encodingTag;
   my $encoding;

   open(my $sconetFile, "<", "$fileName") or die("Cannot open $fileName");
   $encodingTag = <$sconetFile>;
   chomp($encodingTag);

   if( ! ($encodingTag =~ m/<\?xml.*\?>/) ) {
      print( "The file given is not a xml file !\n");
      exit(1);
   }

   if ( $encodingTag =~ /encoding=\"(.*?)\"/ ) {
      $encoding = $1;
   } else {
      $encoding = 'utf8';
   }

   seek($sconetFile, 0, 0);

   # parse the file $
   my $handler = MyHandler->new;
   my $parser = XML::SAX::ParserFactory->parser();
   $parser->parse(Handler => $handler, Source => { ByteStream => $sconetFile, Encoding => "$encoding" } );

   # remove students not affected in classes #
   # and save login #
   my %students = %MyHandler::SCONETStudents;
   
   foreach my $key (keys %students) {
      if ( ! defined($students{$key}{class}) ) {
         delete($students{$key});
      } else {
         $students{$key}{login} =  getLogin( $students{$key}{surName}, $students{$key}{givenName} );
         $students{$key}{completeName} = $students{$key}{surName}. " " . $students{$key}{givenName};
      }
   }

   # return #
   close($sconetFile);
   return %students;
}

#####################
#      MAIN         #
#####################
#####################

###################
# get dabatabases #
###################
print "Reading SCONET database... \n";
my %SCONETStudents = GetSCONETStudents("$ARGV[0]");
print "Reading Samba database... \n";
my @LDBStudents = GetLDBStudents();
print "Comparing databases... \n";

####################################
# try to identify sconet students  #
# already present in ldb database  #
####################################

#--------------------------------#
# 1) first try with national id  #
#--------------------------------#
foreach my $key (keys %SCONETStudents) {

   # check if sconet student already associated #
   if( defined($SCONETStudents{$key}{ldbIndex}) ) {
      next;
   }

   my @findIdx;

   # check if national id defined #
   if( defined($SCONETStudents{$key}{id}) ) {

      # search ldb student with same ID not already associated #
      my $id = $SCONETStudents{$key}{id}; 
      @findIdx = grep { ! defined($LDBStudents[$_]{sconetKey}) && defined($LDBStudents[$_]{id}) && $LDBStudents[$_]{id} eq $id } 0..$#LDBStudents;

      # if only one match, save #
      if( @findIdx == 1 ) {
         $SCONETStudents{$key}{ldbIndex} = $findIdx[0];
         $LDBStudents[$findIdx[0]]{sconetKey} = $key;
      }
   }
}

#----------------------------------------#
# 2) try with good surname and givenname #
#----------------------------------------#
foreach my $key (keys %SCONETStudents) {

   # check if sconet student already associated #
   if( defined($SCONETStudents{$key}{ldbIndex}) ) {
      next;
   }

   my @findIdx;

   # search ldb student not already associated #
   # with good surname AND givenname #
   @findIdx = grep { ! defined($LDBStudents[$_]{sconetKey}) &&
                     ( defined($LDBStudents[$_]{surName}) && removeSpecials($LDBStudents[$_]{surName}) eq removeSpecials($SCONETStudents{$key}{surName}) ) &&
                     ( defined($LDBStudents[$_]{givenName}) && removeSpecials($LDBStudents[$_]{givenName}) eq removeSpecials($SCONETStudents{$key}{givenName}) ) 
                   } 0..$#LDBStudents;

   # if only one match, save #
   if( @findIdx == 1 ) {
      $SCONETStudents{$key}{ldbIndex} = $findIdx[0];
      $LDBStudents[$findIdx[0]]{sconetKey} = $key;
   }
}

#--------------------------------------------#
# 3) try with local id + another good param  #
#--------------------------------------------#
foreach my $key (keys %SCONETStudents) {

   # check if sconet student already associated #
   if( defined($SCONETStudents{$key}{ldbIndex}) ) {
      next;
   }

   my @findIdx;

   # search ldb student not already associated #
   # with a good local ID #
   # and at least one good login, surname, givenname or class #
   my $localId = $key;
   my $login = $SCONETStudents{$key}{login};
   my $studentClass = $SCONETStudents{$key}{class};

   @findIdx = grep { ! defined($LDBStudents[$_]{sconetKey}) &&
                     ( defined($LDBStudents[$_]{localId}) && $LDBStudents[$_]{localId} eq $localId ) &&
                     ( ($LDBStudents[$_]{login} =~ m/^$login[0-9]*$/) ||
                       (defined($LDBStudents[$_]{surName}) && removeSpecials($LDBStudents[$_]{surName}) eq removeSpecials($SCONETStudents{$key}{surName}) ) ||
                       (defined($LDBStudents[$_]{givenName}) && removeSpecials($LDBStudents[$_]{givenName}) eq removeSpecials($SCONETStudents{$key}{givenName}) ) ||
                       ($LDBStudents[$_]{class} eq $studentClass) ) } 0..$#LDBStudents; 

   # if only one match, save #
   if( @findIdx == 1 ) {
      $SCONETStudents{$key}{ldbIndex} = $findIdx[0];
      $LDBStudents[$findIdx[0]]{sconetKey} = $key;
   }
}

#----------------------------------------#
# 4) try with login + another good param #
#----------------------------------------#
foreach my $key (keys %SCONETStudents) {

   # check if sconet student already associated #
   if( defined($SCONETStudents{$key}{ldbIndex}) ) {
      next;
   }

   my @findIdx;

   # search ldb student not already associated #
   # with coherent login #
   # and good surname, givename or class #
   my $login = $SCONETStudents{$key}{login};
   my $studentClass = $SCONETStudents{$key}{class};

   @findIdx = grep { ! defined($LDBStudents[$_]{sconetKey}) &&
                     $LDBStudents[$_]{login} =~ m/^$login[0-9]*$/ &&
                     ( ( defined($LDBStudents[$_]{surName}) && removeSpecials($LDBStudents[$_]{surName}) eq removeSpecials($SCONETStudents{$key}{surName}) ) ||
                       ( defined($LDBStudents[$_]{givenName}) && removeSpecials($LDBStudents[$_]{givenName}) eq removeSpecials($SCONETStudents{$key}{givenName}) ) ||
                       ($LDBStudents[$_]{class} eq $studentClass) ) } 0..$#LDBStudents;

   # if only one match, save #
   if( @findIdx == 1 ) {
      $SCONETStudents{$key}{ldbIndex} = $findIdx[0];
      $LDBStudents[$findIdx[0]]{sconetKey} = $key;
   }
}

#------------------------#
# 5) try with just login #
#------------------------#
foreach my $key (keys %SCONETStudents) {

   # check if sconet student already associated #
   if( defined($SCONETStudents{$key}{ldbIndex}) ) {
      next;
   }

   my @findIdx;

   # search ldb student not already associated #
   # with coherent login #
   my $login = $SCONETStudents{$key}{login};

   @findIdx = grep { ! defined($LDBStudents[$_]{sconetKey}) &&
                     $LDBStudents[$_]{login} =~ m/^$login[0-9]*$/ } 0..$#LDBStudents;

   # if only one match, save #
   if( @findIdx == 1 ) {
      $SCONETStudents{$key}{ldbIndex} = $findIdx[0];
      $LDBStudents[$findIdx[0]]{sconetKey} = $key;
   }
}


######################################
######################################
# FINAL check                        #
#                                    #
# check if we need                   #
# - to ADD, DELETE class             #
# - to ADD, MOVE, or DELETE students #
######################################

#################
# Check classes #
#################

# get the set of classes in the sconet database #
my %sconetClasses;

foreach my $key (keys %SCONETStudents) {
   $sconetClasses{ $SCONETStudents{$key}{class} } = 'true';
}

# get the set of ldb student classes #
my %ldbClasses;

open(my $ldbCall, "-|:encoding(UTF-8)", "s4ldbsearch $studentBaseDN one ou '()' ou") or die("Cannot launch s4ldbsearch command !\n");
while( my $line = <$ldbCall> ) {
   chomp($line);
   my $class = (split(/\t/, $line, -1))[1];
   $ldbClasses{$class} = 'true';
}
close($ldbCall);

# check if we need to create classes #
my @createClasses;

foreach my $class (keys %sconetClasses) {
   if( ! defined($ldbClasses{$class}) ) {
      push(@createClasses, $class);
   }
}

if ( @createClasses > 0 ) {
   print("------------------------------------------\n");
   print("The following classes need to be created :\n");
   print("------------------------------------------\n");
   foreach my $class (@createClasses) {
      print( "$class\n");
   }
   print("\n");
}


# check if we need to delete classes #
my @deleteClasses;

foreach my $class (keys %ldbClasses) {
   if( ! defined($sconetClasses{$class}) ) {
      push(@deleteClasses, $class);
   }
}

if ( $deleteStudents && @deleteClasses > 0 ) {
   print("------------------------------------------\n");
   print("The following classes need to be deleted :\n");
   print("------------------------------------------\n");
   foreach my $class (@deleteClasses) {
      print( "$class\n");
   }
   print("\n");
}


##################
# Check students #
################## 

##########
# DELETE #
##########

# students to delete # 
my @deleteIdx ;

if( $deleteStudents ) {

   foreach my $ldbStudentIdx (0..$#LDBStudents) {
      if( ! defined($LDBStudents[$ldbStudentIdx]{sconetKey}) ) {

         # add student to the delete list #
         push @deleteIdx, $ldbStudentIdx;

         # free the login #
         my $userLogin = $LDBStudents[$ldbStudentIdx]{login};
         delete($usedLogin{$userLogin});
      }
   }

   # print result #
   if ( @deleteIdx > 0 ) {
      print("-------------------------------------------\n");
      print("The following students need to be deleted :\n");
      print("-------------------------------------------\n");
      foreach my $idx (@deleteIdx) {

         # save the delete action #
         $LDBStudents[$idx]{delete} = 'true';

         if ( defined($LDBStudents[$idx]{surName}) && defined($LDBStudents[$idx]{givenName}) ) {
            print( "$LDBStudents[$idx]{surName} $LDBStudents[$idx]{givenName}: login=$LDBStudents[$idx]{login}, class=$LDBStudents[$idx]{class}\n");
         } else {
            print( "login=$LDBStudents[$idx]{login}, class=$LDBStudents[$idx]{class}\n");
         }
      }
      print("\n");
   }
}


########
# ADD  #
########

# students to add #
my @createKeys = grep {  ! defined($SCONETStudents{$_}{ldbIndex})  } keys %SCONETStudents;

#-----------------------------------#
# open the password file if needed #
my $passwordFile;
my @usersPasswords;

if( defined($optPasswordFile) && -e $optPasswordFile ) {
   # read the password file entierely #
   open($passwordFile, "<:encoding(UTF-8)", "$optPasswordFile") or die("Cannot open password file $optPasswordFile");
   @usersPasswords = <$passwordFile>;
   close($passwordFile);
}

sub getPasswordLine {
   my ($class, $givenName, $surName) = @_;

   # input search tag #
   my $inputTag = removeSpecials($givenName).removeSpecials($surName);

   # what we search for #
   my $foundlineIdx;
   my $foundLogin;
   my $foundPassword;

   # check with the three params #
   my @matchLineIdx; # in the case we can't use the class, the lines where givenName, surName match
   foreach my $lineIdx (0..$#usersPasswords) {
      my ($passwordClass, $passwordGivenName, $passwordSurName) = split(/,/, $usersPasswords[$lineIdx], -1);
      my $passwordTag = removeSpecials($passwordGivenName).removeSpecials($passwordSurName);
      if( $inputTag eq $passwordTag ) {
         push(@matchLineIdx, $lineIdx);
         if( $class eq $passwordClass ) {
            $foundlineIdx = $lineIdx;
            last;
         }
      }
   }

   # if not found, try with only the givenName and Surname #
   # check if there are only one result #
   if( ! defined($foundlineIdx) && @matchLineIdx == 1 ) {
      ($foundlineIdx) = @matchLineIdx;
   }

   # if line found get the other params #
   if( defined($foundlineIdx) ) {
      ($foundLogin, $foundPassword) = (split(/,/, $usersPasswords[$foundlineIdx], -1))[3,4];
      chomp($foundPassword);
      if( $foundLogin =~ m/^\s*$/ ) {
         $foundLogin = undef;
      }
      if( $foundPassword =~ m/^\s*$/ ) {
         $foundPassword = undef;
      }
   }

   # return the result #
   # may be undef #
   return ($foundlineIdx, $foundLogin, $foundPassword);
}

sub setPasswordLine {
   my ($lineIdx, $class, $givenName, $surName, $login, $pass) = @_;

   my $passwordLine = "$class,$givenName,$surName,$login,$pass\n";
   if( defined($lineIdx) ) {
     $usersPasswords[$lineIdx] = $passwordLine;
   } else {
     push( @usersPasswords, $passwordLine);
   }
}
#-----------------------------------#

# create password generator if needed #
my $passwordGenerator;
if( defined($optRandomPassword) ) {
   $passwordGenerator = String::Random->new;
}

# foreach new users #
# create login and password #
foreach my $key (@createKeys) {

   #-----------------------------------#
   # if password file, get proposed
   # login and password 
   my $proposedLineIdx = undef;
   my $proposedLogin = undef;
   my $proposedPassword = undef;
   if( $optPasswordFile ) {
      ($proposedLineIdx, $proposedLogin, $proposedPassword) = getPasswordLine($SCONETStudents{$key}{class},
                                                                              $SCONETStudents{$key}{givenName},
                                                                              $SCONETStudents{$key}{surName});
   }
   #-----------------------------------#

   ####################
   # login generation #
   ####################

   # if proposed login #
   # check if it not already used #
   if( defined($proposedLogin) ) {

      # if proposed login is free, use it #
      if( ! defined($usedLogin{$proposedLogin}) ) {
         $SCONETStudents{$key}{newLogin} = $proposedLogin;
         $usedLogin{$proposedLogin} = 'true';
      }
   }

   # if we can't use the proposed login #
   # generate it #
   if( ! defined( $SCONETStudents{$key}{newLogin} ) ) {

      # get simplest login #
      my $baseLogin = $SCONETStudents{$key}{login};
      my $currentLogin = $baseLogin;
      my $currentLoginNumber = 1;

      # increase the login number while the login is used # 
      while( 1 ) {

         # if the current login is free, use it #
         if( ! defined($usedLogin{$currentLogin}) ) {
            $SCONETStudents{$key}{newLogin} = $currentLogin;
            $usedLogin{$currentLogin} = 'true';
            last;
         }

         # else try next login #
         else {
            $currentLoginNumber++;
            $currentLogin = "${baseLogin}${currentLoginNumber}"
         }
      }
   }

   #######################
   # password generation #
   #######################
   if( defined($proposedPassword) ) {
      $SCONETStudents{$key}{newPassword} = $proposedPassword;
   }
   elsif( defined($optRandomPassword) ) {
      $SCONETStudents{$key}{newPassword} = $passwordGenerator->randregex("[a-z]{$loginSurnameChar}[0-9]{$loginGivennameChar}");
   } else {
      $SCONETStudents{$key}{newPassword} = $SCONETStudents{$key}{newLogin}
   }  

   ###############
   # save result #
   ###############
   if( defined($optPasswordFile) ) {
      setPasswordLine($proposedLineIdx,
                      $SCONETStudents{$key}{class},
                      $SCONETStudents{$key}{givenName},
                      $SCONETStudents{$key}{surName},
                      $SCONETStudents{$key}{newLogin},
                      $SCONETStudents{$key}{newPassword} );
   }
}

# print result #
if ( @createKeys > 0 ) {
   print("-------------------------------------------\n");
   print("The following students need to be created :\n");
   print("-------------------------------------------\n");
   foreach my $key (@createKeys) {
      print( "$SCONETStudents{$key}{completeName}: login=$SCONETStudents{$key}{newLogin}, class=$SCONETStudents{$key}{class}\n" );
   }
   print("\n");
}


########
# MOVE #
########

# students to move #
my @moveKeys = grep { defined($SCONETStudents{$_}{ldbIndex}) && ($SCONETStudents{$_}{class} ne $LDBStudents[$SCONETStudents{$_}{ldbIndex}]{class}) } keys %SCONETStudents;

# get the real login #
foreach my $key (@moveKeys) {
   $SCONETStudents{$key}{login} = $LDBStudents[$SCONETStudents{$key}{ldbIndex}]{login};
}

# print result #
if ( @moveKeys > 0 ) {
   print("----------------------------------------------------------\n");
   print("We need to change the class of the the following students:\n");
   print("----------------------------------------------------------\n");
   foreach my $key (@moveKeys) {
      print( "$SCONETStudents{$key}{completeName} ($SCONETStudents{$key}{login}): $LDBStudents[$SCONETStudents{$key}{ldbIndex}]{class} => $SCONETStudents{$key}{class}\n" );
   }
   print("\n");
}

###############
# UPDATE DATA #
###############
my $studentsDataUpdated;

foreach my $key (keys %SCONETStudents) {
  
   # for the new users the data will be automatically updated #
   if( ! defined($SCONETStudents{$key}{newLogin}) && defined($SCONETStudents{$key}{ldbIndex}) ) {

      my $ldbIndex = $SCONETStudents{$key}{ldbIndex};
      
      # check id #
      # normally should never be changed #
      if( (  (! defined($LDBStudents[$ldbIndex]{id})) &&  defined($SCONETStudents{$key}{id})   ) ||
             (  defined($LDBStudents[$ldbIndex]{id}) && defined($SCONETStudents{$key}{id}) && ($LDBStudents[$ldbIndex]{id} ne $SCONETStudents{$key}{id})   )  ) {
            $SCONETStudents{$key}{setId} = 1;
            $studentsDataUpdated = 1;
      }

      # check localId #
      if( (  (! defined($LDBStudents[$ldbIndex]{localId}))  ) ||
             (  (defined($LDBStudents[$ldbIndex]{localId})) && ($LDBStudents[$ldbIndex]{localId} ne $key)   )  ) {
            $SCONETStudents{$key}{setLocalId} = 1;
            $studentsDataUpdated = 1;
      }

      # only if the user want #
      if( $checkStudentData ) {

         # check name #
         if( (!defined($LDBStudents[$ldbIndex]{surName})) ||
             (! defined($LDBStudents[$ldbIndex]{givenName})) ||
             (! defined($LDBStudents[$ldbIndex]{completeName})) || 
             ($LDBStudents[$ldbIndex]{surName} ne $SCONETStudents{$key}{surName}) ||
             ($LDBStudents[$ldbIndex]{givenName} ne $SCONETStudents{$key}{givenName}) ||
             ($LDBStudents[$ldbIndex]{completeName} ne $SCONETStudents{$key}{completeName}) ) {
            $SCONETStudents{$key}{setName} = 1;
            $studentsDataUpdated = 1;
         }

         # check mail #
         if( (  (! defined($LDBStudents[$ldbIndex]{mail})) &&  defined($SCONETStudents{$key}{mail})   ) ||
             (  (defined($LDBStudents[$ldbIndex]{mail})) && defined($SCONETStudents{$key}{mail}) && ($LDBStudents[$ldbIndex]{mail} ne $SCONETStudents{$key}{mail})   )  ) {
            $SCONETStudents{$key}{setMail} = 1;
            $studentsDataUpdated = 1;
         }

         # check telephone number #
         if( ((! defined($LDBStudents[$ldbIndex]{telephoneNumber})) && defined($SCONETStudents{$key}{telephoneNumber})) || 
             ((defined($LDBStudents[$ldbIndex]{telephoneNumber})) && defined($SCONETStudents{$key}{telephoneNumber}) &&  ($LDBStudents[$ldbIndex]{telephoneNumber} ne $SCONETStudents{$key}{telephoneNumber}) )  ) {
            $SCONETStudents{$key}{setTelephoneNumber} = 1;
            $studentsDataUpdated = 1;    
         }
      }
   }
}

# print result #
if( $studentsDataUpdated ) { 
   print("------------------------------------------------\n");
   print("The following students data need to be updated :\n");
   print("------------------------------------------------\n");
   foreach my $key (keys %SCONETStudents) {

      if( $SCONETStudents{$key}{setId} ||
          $SCONETStudents{$key}{setLocalId} ||
          $SCONETStudents{$key}{setName} ||
          $SCONETStudents{$key}{setMail} ||
          $SCONETStudents{$key}{setTelephoneNumber} ) {

         print("$SCONETStudents{$key}{completeName} ($LDBStudents[$SCONETStudents{$key}{ldbIndex}]{login}): \n");
         my $ldbIndex = $SCONETStudents{$key}{ldbIndex};
      
         # setId
         if( $SCONETStudents{$key}{setId} ) {
            print("   - id => $SCONETStudents{$key}{id}\n");
         }

         # setLocalId
         if( $SCONETStudents{$key}{setLocalId} ) {
            print("   - localId => $key\n");
         }

         # setName #
         if( $SCONETStudents{$key}{setName} ) {
            # surname #
            print("   - surname ");
            if( defined($LDBStudents[$ldbIndex]{surName}) ) {
               print( "($LDBStudents[$ldbIndex]{surName})");
            }
            print("=> $SCONETStudents{$key}{surName}\n");
            # givenName #
            print("   - givenname ");
            if( defined($LDBStudents[$ldbIndex]{givenName}) ) {
               print( "($LDBStudents[$ldbIndex]{givenName})");
            }
            print("=> $SCONETStudents{$key}{givenName}\n");
            # completeName #
            print("   - complete name ");
            if( defined($LDBStudents[$ldbIndex]{completeName}) ) {
               print( "($LDBStudents[$ldbIndex]{completeName})");
            }
            print("=> $SCONETStudents{$key}{completeName}\n");
         }

         # setMail #
         if( $SCONETStudents{$key}{setMail} ) {
            print("   - mail ");
            if( defined($LDBStudents[$ldbIndex]{mail}) ) {
               print( "($LDBStudents[$ldbIndex]{mail})");
            }
            print("=> $SCONETStudents{$key}{mail}\n");
         }

         # setTelephoneNumber #
         if( $SCONETStudents{$key}{setTelephoneNumber} ) {
            print("   - telephone number ");
            if( defined($LDBStudents[$ldbIndex]{telephoneNumber}) ) {
               print( "($LDBStudents[$ldbIndex]{telephoneNumber})");
            }
            print("=> $SCONETStudents{$key}{telephoneNumber}\n");
         }
      }
   }
   print("\n");
}

####################################
####################################
#                                  #
#        SAMBA UPDATE              #
#                                  #
####################################
####################################

########################
# if needed create the #
#  output script       #
########################
my $updateScript;

if( $optUpdateScript ) {
   open($updateScript, ">:encoding(UTF-8)", "$optUpdateScript") or die("Cannot create script file $optUpdateScript");
   print $updateScript "\#! /bin/bash\n";
   print $updateScript "\n";
   chmod(0755, $optUpdateScript); 
}

sub doUpdate {
   my ($msg, $cmd) = @_;

   # update script case #
   if( defined($optUpdateScript) ) {
      print $updateScript "echo \"$msg\"\n";
      print $updateScript "$cmd\n";
   }
   # direct update #
   else {
      print("$msg\n");
      system("$cmd");  
   }
}


#######################
# maybe nothing to do #
#######################
if( ! @createClasses && (! $deleteStudents || ! @deleteClasses) && (! $deleteStudents || ! @deleteIdx) && ! @createKeys && ! @moveKeys && ! $studentsDataUpdated ) {
   print("---------------------\n");
   print(" Nothing to update ! \n");
   print("---------------------\n");
   exit(0);
}


##################
# ask for update #
##################

# never update directly without user action #
# if the script is not interactive the user need #
# to output a script #
if( defined($optNoInteractive) && ! defined($optUpdateScript) ) {
   print("Changes are not applied without user confimation !\n");
   print("Remove the no interactive option or output a script.\n");
   exit 0;
}

# check if interaction #
if( askYesOrNo("Would you like to apply there changes ?") ne "yes" ) {
   exit(0);
}

##################
# open ldif file #
##################
my $ldifFileName = "samba".int(rand(10000)).".ldif";
my $ldifFile;

if( ! open($ldifFile, ">:encoding(UTF-8)", "/tmp/$ldifFileName") ) {
   print("error : can't create ldif file in /tmp \n");
   exit(1);
}


#################
# DEL students  #
# FIRST !!!!!   #
#################
if( $deleteStudents ) {
   foreach my $idx (@deleteIdx) {

      # delete student #
      doUpdate("--- Deleting student $LDBStudents[$idx]{login} ---",
               "s4studentdel $LDBStudents[$idx]{login}");
   }
}

##################
# delete classes #
##################
if( $deleteStudents ) {
   foreach my $class (@deleteClasses) {
      doUpdate("--- Deleting class $class ---",
               "s4classdel -f $class");
   }
}


##################
# create classes #
##################
foreach my $class (@createClasses) {
   doUpdate("--- Creating class $class ---",
            "s4studentclassadd $class");
}


################
# ADD students #
################
foreach my $key (@createKeys) {

   # create student #
   if( $SCONETStudents{$key}{newPassword} ) { 
      doUpdate("--- Creating student $SCONETStudents{$key}{newLogin} on class $SCONETStudents{$key}{class} ---",
               "s4studentadd -p $SCONETStudents{$key}{newPassword} $SCONETStudents{$key}{newLogin} $SCONETStudents{$key}{class}");
   } else {
      doUpdate("--- Creating student $SCONETStudents{$key}{newLogin} on class $SCONETStudents{$key}{class} ---",
               "s4studentadd $SCONETStudents{$key}{newLogin} $SCONETStudents{$key}{class}");
   }

   # write ldif dn entry #
   print( $ldifFile "dn: CN=$SCONETStudents{$key}{newLogin},OU=".uc($SCONETStudents{$key}{class}).",$studentBaseDN\n");
   print( $ldifFile "changetype: modify\n");
   
   # write data #
   if( defined($SCONETStudents{$key}{id}) ) {
      print( $ldifFile "replace: serialNumber\n");
      print( $ldifFile "serialNumber: $SCONETStudents{$key}{id}\n");
   }
   
   print( $ldifFile "replace: employeeNumber\n");
   print( $ldifFile "employeeNumber: $key\n");

   if( defined($SCONETStudents{$key}{surName}) ) {
      print( $ldifFile "replace: sn\n");
      print( $ldifFile "sn: $SCONETStudents{$key}{surName}\n");
   }

   if( defined($SCONETStudents{$key}{givenName}) ) {   
      print( $ldifFile "replace: givenName\n");
      print( $ldifFile "givenName: $SCONETStudents{$key}{givenName}\n");
   }

   if( defined($SCONETStudents{$key}{completeName}) ) {
      print( $ldifFile "replace: displayName\n");
      print( $ldifFile "displayName: $SCONETStudents{$key}{completeName}\n");
   }

   if( defined($SCONETStudents{$key}{telephoneNumber}) ) {
      print( $ldifFile "replace: telephoneNumber\n");
      print( $ldifFile "telephoneNumber: $SCONETStudents{$key}{telephoneNumber}\n");
   }

   if( defined($SCONETStudents{$key}{mail}) ) {
      print( $ldifFile "replace: mail\n");
      print( $ldifFile "mail: $SCONETStudents{$key}{mail}\n");
   }

   # next student #
   print( $ldifFile "\n");
}

#################
# MOVE students #
#################
foreach my $key (@moveKeys) {

   # move student #
   doUpdate("--- Moving student $SCONETStudents{$key}{login} to class $SCONETStudents{$key}{class} ---",
            "s4studentmove $SCONETStudents{$key}{login} $SCONETStudents{$key}{class}");
}


#########################
# update student's data #
#########################
foreach my $key (keys %SCONETStudents) {

   if( $SCONETStudents{$key}{setId} ||
       $SCONETStudents{$key}{setLocalId} ||
       $SCONETStudents{$key}{setName} ||
       $SCONETStudents{$key}{setMail} ||
       $SCONETStudents{$key}{setTelephoneNumber} ) {

      # get the real login #
      my $login = $LDBStudents[$SCONETStudents{$key}{ldbIndex}]{login};

      # write ldif dn entry #
      print( $ldifFile "dn: CN=$login,OU=".uc($SCONETStudents{$key}{class}).",$studentBaseDN\n");
      print( $ldifFile "changetype: modify\n");

      # setId #
      if( $SCONETStudents{$key}{setId} ) {
         print( $ldifFile "replace: serialNumber\n");
         print( $ldifFile "serialNumber: $SCONETStudents{$key}{id}\n");
      }

      # setLocalId #
      if( $SCONETStudents{$key}{setLocalId} ) {
         print( $ldifFile "replace: employeeNumber\n");
         print( $ldifFile "employeeNumber: $key\n");
      }

      # setName #
      if( $SCONETStudents{$key}{setName} ) {
         print( $ldifFile "replace: sn\n");
         print( $ldifFile "sn: $SCONETStudents{$key}{surName}\n");
         print( $ldifFile "replace: givenName\n");
         print( $ldifFile "givenName: $SCONETStudents{$key}{givenName}\n");
         print( $ldifFile "replace: displayName\n");
         print( $ldifFile "displayName: $SCONETStudents{$key}{completeName}\n");

      }

      # setMail #
      if( $SCONETStudents{$key}{setMail} ) {
         print( $ldifFile "replace: mail\n");
         print( $ldifFile "mail: $SCONETStudents{$key}{mail}\n");
      }
   
      # setTelephoneNumber #
      if( $SCONETStudents{$key}{setTelephoneNumber} ) {
         print( $ldifFile "replace: telephoneNumber\n");
         print( $ldifFile "telephoneNumber: $SCONETStudents{$key}{telephoneNumber}\n");
      }

      # next student #
      print( $ldifFile "\n");
   }
}

##############
# apply LDIF #
##############
close($ldifFile);

if( ! $optUpdateScript ) {
   doUpdate("--- Applying ldif /tmp/$ldifFileName ... ---",
            "ldbmodify -H $samDatabase /tmp/$ldifFileName");
}
# else put the ldif file in the srcipt #
else {
   print $updateScript "echo \"--- Applying ldif /tmp/$ldifFileName ... ---\"\n";
   print $updateScript "ldbmodify -H $samDatabase <<EOFLDIFS\n";
   close($updateScript);
   system("cat /tmp/$ldifFileName >> $optUpdateScript");
   system("echo \"EOFLDIFS\" >> $optUpdateScript");
} 

#######################
# sort and            #
# write password file # 
# if needed           #
#######################
sub csvCmp {
   my @fieldsA = split(/,/, $a, -1);
   my @fieldsB = split(/,/, $b, -1);
   my $i = 0;
   while( defined($fieldsA[$i]) && defined($fieldsB[$i]) ) {
      if( my $compResult = $fieldsA[$i] cmp $fieldsB[$i] ) {
         return $compResult;
      } else {       
         $i++;
      }
   }

   # check #
   if( ! defined($fieldsA[$i]) && defined($fieldsB[$i]) ) {
      return -1;
   }
   elsif( defined($fieldsA[$i]) && ! defined($fieldsB[$i]) ) {
      return 1;
   }
   else {
      return 0;
   }
}

if( $optPasswordFile ) {
   # sort by fields #
   @usersPasswords = sort csvCmp @usersPasswords;

   # write to file #  
   open($passwordFile, ">:encoding(UTF-8)", "$optPasswordFile") or die("Cannot open password file $optPasswordFile");
   print $passwordFile @usersPasswords;
   close($passwordFile);
}


print("----------------------------\n");
print(" Student database updated ! \n");
print("----------------------------\n");


#---------------------------------

#######################
# xml parser package  #
#######################
package MyHandler;
use base qw(XML::SAX::Base);

%MyHandler::SCONETStudents = ();
@MyHandler::currentElement = ();
$MyHandler::currentID = "";
$MyHandler::currentClass = "";
$MyHandler::currentClassType = "";

sub start_element {
   my $self = shift;
   my $data = shift;

   our %SCONETStudents;
   our @currentElement;
   our $currentID;
   our $currentClass;
   our $currentClassType;


   # save the Name #
   push(@currentElement, $data->{Name});

   ########################
   # we get ID from there #
   ########################
   if( $data->{Name} eq "ELEVE" || $data->{Name} eq "STRUCTURES_ELEVE"){
      my $attributes =  $data->{Attributes};
      foreach my $key (keys %{$attributes}) {
         my $attribute = $attributes->{$key};
         if ( $attribute->{Name} eq "ELEVE_ID" ) {
            $currentID =  $attribute->{Value};
         }
      }
   }

   ###########################
   # empty current variables #
   ###########################
   if ( @currentElement < 3 ) {
      return;
   }

   # inside class #
   if ( @currentElement[-3..-1] ~~ ["STRUCTURES_ELEVE","STRUCTURE","CODE_STRUCTURE"] ) {
      $currentClass = "";
   }
   # inside class type #
   elsif ( @currentElement[-3..-1] ~~ ["STRUCTURES_ELEVE","STRUCTURE","TYPE_STRUCTURE"] ) {
      $currentClassType = "";
   }

}

sub end_element {
   my $self = shift;
   my $data = shift;

   our %SCONETStudents;
   our @currentElement;
   our $currentID;
   our $currentClass;
   our $currentClassType;

   # class structure have multiple parameters #
   if ( (@currentElement >= 3) && (@currentElement[-2..-1] ~~ ["STRUCTURES_ELEVE","STRUCTURE"] )) {
      if ( $currentClassType eq "D" ) {
        $SCONETStudents{$currentID}{class} = $currentClass;
      }
   }

   pop(@currentElement);
}

sub characters {
   my $self = shift;
   my $data = shift;

   our %SCONETStudents;
   our @currentElement;
   our $currentID;
   our $currentClass;
   our $currentClassType;

   if ( @currentElement < 3 ) {
      return;
   }

   # inside NOM #
   if ( @currentElement[-2..-1] ~~ ["ELEVE","NOM"]  ) {
      $SCONETStudents{$currentID}{surName} = defined($SCONETStudents{$currentID}{surName}) ? $SCONETStudents{$currentID}{surName} . $data->{Data} : $data->{Data};
   }
   # inside NOM_DE_FAMILLE #
   if ( @currentElement[-2..-1] ~~ ["ELEVE","NOM_DE_FAMILLE"]  ) {
      $SCONETStudents{$currentID}{surName} = defined($SCONETStudents{$currentID}{surName}) ? $SCONETStudents{$currentID}{surName} . $data->{Data} : $data->{Data};
   }
   # inside PRENOM #
   elsif ( @currentElement[-2..-1] ~~ ["ELEVE","PRENOM"] ) {
      $SCONETStudents{$currentID}{givenName} = defined($SCONETStudents{$currentID}{givenName}) ? $SCONETStudents{$currentID}{givenName} . $data->{Data} : $data->{Data};
   }
   # inside ID_NATIONAL #
   elsif ( @currentElement[-2..-1] ~~ ["ELEVE","ID_NATIONAL"] ) {
      $SCONETStudents{$currentID}{id} = defined($SCONETStudents{$currentID}{id}) ? $SCONETStudents{$currentID}{id} . lc($data->{Data}) : lc($data->{Data});
   }
   # inside MEL #
   elsif ( @currentElement[-2..-1] ~~ ["ELEVE","MEL"] ) {
      $SCONETStudents{$currentID}{mail} = defined($SCONETStudents{$currentID}{mail}) ? $SCONETStudents{$currentID}{mail} . lc($data->{Data}) : lc($data->{Data});
   }
   # inside TEL_PORTABLE #
   elsif ( @currentElement[-2..-1] ~~ ["ELEVE","TEL_PORTABLE"] ) {
      my $teln = $data->{Data};
      $teln =~ s/\s+//g;
      $SCONETStudents{$currentID}{telephoneNumber} = defined($SCONETStudents{$currentID}{telephoneNumber}) ? $SCONETStudents{$currentID}{telephoneNumber} . $teln : $teln;
   }
   # inside class #
   elsif ( @currentElement[-3..-1] ~~ ["STRUCTURES_ELEVE","STRUCTURE","CODE_STRUCTURE"] ) {
      my $class = $data->{Data};
      $class =~ s/\s+//g;
      #$SCONETStudents{$currentID}{class} = lc($class);
      $currentClass = $currentClass . lc($class);
   }
   # inside class type #
   elsif ( @currentElement[-3..-1] ~~ ["STRUCTURES_ELEVE","STRUCTURE","TYPE_STRUCTURE"] ) {
      my $type = $data->{Data};
      $type =~ s/\s+//g;
      $currentClassType = $currentClassType . $type;
   }
}


